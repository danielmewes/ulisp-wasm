; Send an email
; http://www.ulisp.com/show?2B22
(defun send (message)
  (let ((println #'(lambda (x s) (format s "~a~a~%" x #\return))))
    (with-client (s "mail.mysite.com" 25)
      (println "EHLO mysite.com" s)
      (println "AUTH LOGIN" s)
      (base64 "david" s) 
      (base64 "secret99" s)
      (println "MAIL FROM:david@mysite.com" s)
      (println "RCPT TO:lisa@othersite.com" s)
      (println "DATA" s)
      (println message s)
      (println "." s)
      (println "QUIT" s))))

; Send an email with feedback to serial monitor
(defun send2 (message)
  (let* ((println #'(lambda (x s) (format s "~a~a~%" x #\return)))
         (talk #'(lambda (x s)
                   (println x s) 
                   (princ x) (terpri)
                   (loop (unless (zerop (available s)) (return)))
                   (loop
                    (princ (read-line s)) (terpri)
                    (when (zerop (available s)) (return))))))
    (with-client (s "mail.mysite.com" 25)
      (talk "EHLO mysite.com" s)
      (talk "AUTH LOGIN" s)
      (base64 "david" s) 
      (base64 "secret99" s)
      (talk "MAIL FROM:david@mysite.com" s)
      (talk "RCPT TO:lisa@othersite.com" s)
      (talk "DATA" s)
      (println message s)
      (talk "." s)
      (talk "QUIT" s))))